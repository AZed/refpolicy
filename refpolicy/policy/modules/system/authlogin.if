## <summary>Common policy for authentication and user login.</summary>

#######################################
## <summary>
##	The per user domain template for the authlogin module.
## </summary>
## <desc>
##	<p>
##	This template creates a derived domain which is allowed
##	to authenticate users by using PAM unix_chkpwd support.
##	This domain will be used by any programs running in the
##	user domain which use PAM to authenticate.
##	</p>
##	<p>
##	This template is invoked automatically for each user, and
##	generally does not need to be invoked directly
##	by policy writers.
##	</p>
## </desc>
## <param name="userdomain_prefix">
##	The prefix of the user domain (e.g., user
##	is the prefix for user_t).
## </param>
#
template(`authlogin_per_userdomain_template',`
	gen_require(`
		attribute can_read_shadow_passwords;
		type chkpwd_exec_t, system_chkpwd_t, shadow_t;
		class file rx_file_perms;
		class process { getattr transition sigchld };
		class capability setuid;
		class unix_stream_socket { create read getattr write setattr append bind connect getopt setopt shutdown }; 
		class unix_dgram_socket { create read getattr write setattr append bind connect getopt setopt shutdown };
		class fd use;
		class fifo_file rw_file_perms;
	')

	type $1_chkpwd_t, can_read_shadow_passwords;
	domain_type($1_chkpwd_t)
	domain_entry_file($1_chkpwd_t,chkpwd_exec_t)
	role $1_r types $1_chkpwd_t;
	role $1_r types system_chkpwd_t;

	allow $1_chkpwd_t self:capability setuid;
	allow $1_chkpwd_t self:process getattr;

	files_list_etc($1_chkpwd_t)
	allow $1_chkpwd_t shadow_t:file { getattr read };

	# is_selinux_enabled
	kernel_read_system_state($1_chkpwd_t)

	fs_dontaudit_getattr_xattr_fs($1_chkpwd_t)

	domain_use_wide_inherit_fd($1_chkpwd_t)

	libs_use_ld_so($1_chkpwd_t)
	libs_use_shared_libs($1_chkpwd_t)

	files_read_etc_files($1_chkpwd_t)
	# for nscd
	files_dontaudit_search_var($1_chkpwd_t)

	logging_send_syslog_msg($1_chkpwd_t)

	miscfiles_read_localization($1_chkpwd_t)

	seutil_read_config($1_chkpwd_t)

	#can_ldap($1_chkpwd_t)

	# Transition from the user domain to this domain.
	domain_auto_trans($1_t,chkpwd_exec_t,$1_chkpwd_t)

	allow $1_chkpwd_t $1_t:fd use;
	allow $1_t $1_chkpwd_t:fd use;
	allow $1_chkpwd_t $1_t:fifo_file rw_file_perms;
	allow $1_chkpwd_t $1_t:process sigchld;

	# Write to the user domain tty.
	#userdomain_use_$1_terminal($1_chkpwd_t)
	#userdomain_use_$1_pty($1_chkpwd_t)

	# Inherit and use descriptors from gnome-pty-helper.
	#ifdef(`gnome-pty-helper.te',`allow $1_chkpwd_t $1_gph_t:fd use;')

	tunable_policy(`use_dns',`
		allow $1_chkpwd_t self:udp_socket create_socket_perms;
		corenet_udp_sendrecv_all_if($1_chkpwd_t)
		corenet_raw_sendrecv_all_if($1_chkpwd_t)
		corenet_udp_sendrecv_all_nodes($1_chkpwd_t)
		corenet_raw_sendrecv_all_nodes($1_chkpwd_t)
		corenet_udp_bind_all_nodes($1_chkpwd_t)
		corenet_udp_sendrecv_dns_port($1_chkpwd_t)
		sysnet_read_config($1_chkpwd_t)
	')

	optional_policy(`kerberos.te',`
		kerberos_use($1_chkpwd_t)
	')

	optional_policy(`nis.te',`
		nis_use_ypbind($1_chkpwd_t)
	')

	optional_policy(`nscd.te',`
		nscd_use_socket($1_chkpwd_t)
	')

	optional_policy(`selinuxutil.te',`
		seutil_use_newrole_fd($1_chkpwd_t)
	')

')

########################################
## <desc>
##	Use the login program as an entry point program.
## </desc>
## <param name="domain">
##	The type of process using the login program as entry point.
## </param>
#
interface(`auth_login_entry_type',`
	gen_require(`
		type login_exec_t;
	')

	domain_entry_file($1,login_exec_t)
')

########################################
## <desc>
##	Execute a login_program in the target domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="target_domain">
##	The type of the login_program process.
## </param>
#
interface(`auth_domtrans_login_program',`
	gen_require(`
		type login_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	corecmd_search_bin($1)
	domain_auto_trans($1,login_exec_t,$2)

	allow $1 $2:fd use;
	allow $2 $1:fd use;
	allow $2 $1:fifo_file rw_file_perms;
	allow $2 $1:process sigchld;
')

########################################
## <desc>
##	Run unix_chkpwd to check a password.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_domtrans_chk_passwd',`
	gen_require(`
		type system_chkpwd_t, chkpwd_exec_t, shadow_t;
		class process sigchld;
		class udp_socket create_socket_perms;
		class fd use;
		class fifo_file rw_file_perms;
	')

	corecmd_search_sbin($1)
	domain_auto_trans($1,chkpwd_exec_t,system_chkpwd_t)

	allow $1 system_chkpwd_t:fd use;
	allow system_chkpwd_t $1:fd use;
	allow system_chkpwd_t $1:fifo_file rw_file_perms;
	allow system_chkpwd_t $1:process sigchld;

	dontaudit $1 shadow_t:file { getattr read };

	tunable_policy(`use_dns',`
		allow $1 self:udp_socket create_socket_perms;
		corenet_udp_sendrecv_all_if($1)
		corenet_raw_sendrecv_all_if($1)
		corenet_udp_sendrecv_all_nodes($1)
		corenet_raw_sendrecv_all_nodes($1)
		corenet_udp_bind_all_nodes($1)
		corenet_udp_sendrecv_dns_port($1)
		sysnet_read_config($1)
	')

	optional_policy(`kerberos.te',`
		kerberos_use($1)
	')

	#can_ldap($1)

	optional_policy(`nis.te',`
		nis_use_ypbind($1)
	')
')

########################################
## <summary>
##	Get the attributes of the shadow passwords file.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_getattr_shadow',`
	gen_require(`
		type shadow_t;
		class file getattr;
	')

	files_search_etc($1)
	allow $1 shadow_t:file getattr;
')

########################################
## <summary>
##	Do not audit attempts to get the attributes
##	of the shadow passwords file.
## </summary>
## <param name="domain">
##	Domain to not audit.
## </param>
#
interface(`auth_dontaudit_getattr_shadow',`
	gen_require(`
		type shadow_t;
		class file getattr;
	')

	dontaudit $1 shadow_t:file getattr;
')

########################################
## <desc>
##	Read the shadow passwords file (/etc/shadow)
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_read_shadow',`
	gen_require(`
		attribute can_read_shadow_passwords;
		type shadow_t;
		class file r_file_perms;
	')

	files_list_etc($1)
	allow $1 shadow_t:file r_file_perms;
	typeattribute $1 can_read_shadow_passwords;
')

########################################
## <desc>
##	Do not audit attempts to read the shadow
##	password file (/etc/shadow).
## </desc>
## <param name="domain">
##	The type of the domain to not audit.
## </param>
#
interface(`auth_dontaudit_read_shadow',`
	gen_require(`
		type shadow_t;
		class file r_file_perms;
	')

	dontaudit $1 shadow_t:file { getattr read };
')

########################################
## <desc>
##	Read and write the shadow password file (/etc/shadow).
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_rw_shadow',`
	gen_require(`
		attribute can_read_shadow_passwords, can_write_shadow_passwords;
		type shadow_t;
		class file rw_file_perms;
	')

	files_list_etc($1)
	allow $1 shadow_t:file rw_file_perms;
	typeattribute $1 can_read_shadow_passwords, can_write_shadow_passwords;
')

#######################################
#
# auth_manage_shadow(domain)
#
interface(`auth_manage_shadow',`
	gen_require(`
		attribute can_read_shadow_passwords, can_write_shadow_passwords;
		type shadow_t;
		class file create_file_perms;
	')

	allow $1 shadow_t:file create_file_perms;
	files_create_etc_config($1,shadow_t,file)

	typeattribute $1 can_read_shadow_passwords, can_write_shadow_passwords;
')

#######################################
#
# auth_relabelto_shadow(domain)
#
interface(`auth_relabelto_shadow',`
	gen_require(`
		attribute can_relabelto_shadow_passwords;
		type shadow_t;
		class file relabelto;
	')

	files_search_etc($1)
	allow $1 shadow_t:file relabelto;
	typeattribute $1 can_relabelto_shadow_passwords;
')

#######################################
#
# auth_rw_faillog(domain)
#
interface(`auth_rw_faillog',`
	gen_require(`
		type faillog_t;
		class file rw_file_perms;
	')

	logging_search_logs($1)
	allow $1 faillog_t:file rw_file_perms;
')

#######################################
#
# auth_rw_lastlog(domain)
#
interface(`auth_rw_lastlog',`
	gen_require(`
		type lastlog_t;
		class file { getattr read write setattr };
	')

	logging_search_logs($1)
	allow $1 lastlog_t:file { getattr read write setattr };
')

########################################
## <desc>
##	Execute pam programs in the pam domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_domtrans_pam',`
	gen_require(`
		type pam_t, pam_exec_t;
		class process sigchld;
		class fd
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,pam_exec_t,pam_t)

	allow $1 pam_t:fd use;
	allow pam_t $1:fd use;
	allow pam_t $1:fifo_file rw_file_perms;
	allow pam_t $1:process sigchld;
')

########################################
## <desc>
##	Execute pam programs in the PAM domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to allow the PAM domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the PAM domain to use.
## </param>
#
interface(`auth_run_pam',`
	gen_require(`
		type pam_t;
		class chr_file rw_file_perms;
	')

	auth_domtrans_pam($1)
	role $2 types pam_t;
	allow pam_t $3:chr_file rw_file_perms;
')

########################################
## <desc>
##	Execute the pam program.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_exec_pam',`
	gen_require(`
		type pam_exec_t;
	')

	can_exec($1,pam_exec_t)
')

#######################################
#
# auth_read_pam_pid(domain)
#
interface(`auth_read_pam_pid',`
	gen_require(`
		type pam_var_run_t;
		class dir r_dir_perms;
		class file r_file_perms;
	')

	files_search_var($1)
	files_search_pids($1)
	allow $1 pam_var_run_t:dir r_dir_perms;
	allow $1 pam_var_run_t:file r_file_perms;
')

########################################
## <desc>
##	Delete pam PID files.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_delete_pam_pid',`
	gen_require(`
		type pam_var_run_t;
		class dir { getattr search read write remove_name };
		class file { getattr unlink };
	')

	files_search_var($1)
	files_search_pids($1)
	allow $1 pam_var_run_t:dir { getattr search read write remove_name };
	allow $1 pam_var_run_t:file { getattr unlink };
')

#######################################
#
# auth_domtrans_pam_console(domain)
#
interface(`auth_domtrans_pam_console',`
	gen_require(`
		type pam_console_t, pam_console_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,pam_console_exec_t,pam_console_t)

	allow $1 pam_console_t:fd use;
	allow pam_console_t $1:fd use;
	allow pam_console_t $1:fifo_file rw_file_perms;
	allow pam_console_t $1:process sigchld;
')

#######################################
#
# auth_list_pam_console_data(domain)
#
interface(`auth_list_pam_console_data',`
	gen_require(`
		type pam_var_console_t;
		class dir r_dir_perms;
	')

	files_search_var($1)
	files_search_pids($1)
	allow $1 pam_var_console_t:dir r_dir_perms;
')

#######################################
#
# auth_read_pam_console_data(domain)
#
interface(`auth_read_pam_console_data',`
	gen_require(`
		type pam_var_console_t;
		class dir r_dir_perms;
		class file r_file_perms;
	')

	files_search_var($1)
	files_search_pids($1)
	allow $1 pam_var_console_t:dir r_dir_perms;
	allow $1 pam_var_console_t:file r_file_perms;
')

#######################################
#
# auth_manage_pam_console_data(domain)
#
interface(`auth_manage_pam_console_data',`
	gen_require(`
		type pam_var_console_t;
		class dir rw_dir_perms;
		class file create_file_perms;
		class lnk_file create_lnk_perms;
	')

	files_search_var($1)
	files_search_pids($1)
	allow $1 pam_var_console_t:dir rw_dir_perms;
	allow $1 pam_var_console_t:file create_file_perms;
	allow $1 pam_var_console_t:lnk_file create_lnk_perms;
')

########################################
## <desc>
##	Relabel all files on the filesystem, except
##	the shadow passwords and listed exceptions.
## </desc>
## <param name="domain">
##	The type of the domain perfoming this action.
## </param>
## <param name="exception_types" optional="true">
##	The types to be excluded.  Each type or attribute
##	must be negated by the caller.
## </param>
#

interface(`auth_relabel_all_files_except_shadow',`
	gen_require(`
		type shadow_t;
	')

	files_relabel_all_files($1,$2 -shadow_t)
')

########################################
## <desc>
##	Manage all files on the filesystem, except
##	the shadow passwords and listed exceptions.
## </desc>
## <param name="domain">
##	The type of the domain perfoming this action.
## </param>
## <param name="exception_types" optional="true">
##	The types to be excluded.  Each type or attribute
##	must be negated by the caller.
## </param>
#

interface(`auth_manage_all_files_except_shadow',`
	gen_require(`
		type shadow_t;
	')

	files_manage_all_files($1,$2 -shadow_t)
')

########################################
## <desc>
##	Execute utempter programs in the utempter domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`auth_domtrans_utempter',`
	gen_require(`
		type utempter_t, utempter_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,utempter_exec_t,utempter_t)

	allow $1 utempter_t:fd use;
	allow utempter_t $1:fd use;
	allow utempter_t $1:fifo_file rw_file_perms;
	allow utempter_t $1:process sigchld;
')

########################################
## <desc>
##	Execute utempter programs in the utempter domain.
## </desc>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to allow the utempter domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the utempter domain to use.
## </param>
#
interface(`auth_run_utempter',`
	gen_require(`
		type utempter_t;
		class chr_file rw_file_perms;
	')

	auth_domtrans_utempter($1)
	role $2 types utempter_t;
	allow utempter_t $3:chr_file rw_file_perms;
')

#######################################
#
# auth_read_login_records(domain)
#
interface(`auth_read_login_records',`
	gen_require(`
		type wtmp_t;
		class file r_file_perms;
	')

	logging_search_logs($1)
	allow $1 wtmp_t:file r_file_perms;
')

#######################################
#
# auth_dontaudit_write_login_records(domain)
#
interface(`auth_dontaudit_write_login_records',`
	gen_require(`
		type wtmp_t;
		class file write;
	')

	dontaudit $1 wtmp_t:file write;
')

#######################################
#
# auth_rw_login_records(domain)
#
interface(`auth_rw_login_records',`
	gen_require(`
		type wtmp_t;
		class file rw_file_perms;
	')

	allow $1 wtmp_t:file rw_file_perms;
	logging_search_logs($1)
')

#######################################
#
# auth_manage_login_records(domain)
#
interface(`auth_manage_login_records',`
	gen_require(`
		type wtmp_t;
		class file create_file_perms;
	')

	logging_rw_log_dir($1)
	allow $1 wtmp_t:file create_file_perms;
')

########################################
## <summary>
##	Unconfined access to the authlogin module.
## </summary>
## <desc>
##	<p>
##	Unconfined access to the authlogin module.
##	</p>
##	<p>
##	Currently, this only allows assertions for
##	the shadow passwords file (/etc/shadow) to
##	be passed.  No access is granted yet.
##	</p>
## </desc>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`auth_unconfined',`
	gen_require(`
		attribute can_read_shadow_passwords;
		attribute can_write_shadow_passwords;
		attribute can_relabelto_shadow_passwords;
	')

	typeattribute $1 can_read_shadow_passwords;
	typeattribute $1 can_write_shadow_passwords;
	typeattribute $1 can_relabelto_shadow_passwords;
')
