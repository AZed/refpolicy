## <summary>List kernel modules of USB devices</summary>

########################################
## <summary>
##	Execute usbmodules in the usbmodules domain.
## </summary>
## <param name="domain">
##	Domain allowed access.
## </param>
#
interface(`usbmodules_domtrans',`
	gen_require(`
		type usbmodules_t, usbmodules_exec_t;
	')

	domain_auto_trans($1, usbmodules_exec_t, usbmodules_t)

	allow $1 usbmodules_t:fd use;
	allow usbmodules_t $1:fd use;
	allow usbmodules_t $1:fifo_file rw_file_perms;
	allow usbmodules_t $1:process sigchld;

')

########################################
## <summary>
##	Execute usbmodules in the usbmodules domain, and
##	allow the specified role the usbmodules domain,
##	and use the caller's terminal.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to be allowed the usbmodules domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the usbmodules domain to use.
## </param>
#
interface(`usbmodules_run',`
	gen_require(`
		type usbmodules_t;
	')

	usbmodules_domtrans($1)
	role $2 types usbmodules_t;
	allow usbmodules_t $3:chr_file rw_term_perms;
')
