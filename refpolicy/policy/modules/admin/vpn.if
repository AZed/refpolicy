## <summary>Virtual Private Networking client</summary>

########################################
## <summary>
##	Execute VPN clients in the vpnc domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`vpn_domtrans',`
	gen_require(`
		type vpnc_t, vpnc_exec_t;
		class process sigchld;
		class fd use;
		class fifo_file rw_file_perms;
	')

	domain_auto_trans($1,vpnc_exec_t,vpnc_t)

	allow $1 vpnc_t:fd use;
	allow vpnc_t $1:fd use;
	allow vpnc_t $1:fifo_file rw_file_perms;
	allow vpnc_t $1:process sigchld;
')

########################################
## <summary>
##	Execute VPN clients in the vpnc domain, and
##	allow the specified role the vpnc domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	The role to be allowed the vpnc domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the vpnc domain to use.
## </param>
#
interface(`vpn_run',`
	gen_require(`
		type vpnc_t;
		class chr_file rw_term_perms;
	')

	vpn_domtrans($1)
	role $2 types vpnc_t;
	allow vpnc_t $3:chr_file rw_term_perms;
')
