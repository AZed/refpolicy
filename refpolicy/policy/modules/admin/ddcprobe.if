## <summary>ddcprobe retrieves monitor and graphics card information</summary>

########################################
## <summary>
##	Execute ddcprobe in the ddcprobe domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
#
interface(`ddcprobe_domtrans',`
	gen_require(`
		type ddcprobe_t, ddcprobe_exec_t;
	')

	domain_auto_trans($1,ddcprobe_exec_t,ddcprobe_t)

	allow $1 ddcprobe_t:fd use;
	allow ddcprobe_t $1:fd use;
	allow ddcprobe_t $1:fifo_file rw_file_perms;
	allow ddcprobe_t $1:process sigchld;
')

########################################
## <summary>
##	Execute ddcprobe in the ddcprobe domain, and
##	allow the specified role the ddcprobe domain.
## </summary>
## <param name="domain">
##	The type of the process performing this action.
## </param>
## <param name="role">
##	Role to be authenticated for ddcprobe domain.
## </param>
## <param name="terminal">
##	The type of the terminal allow the clock domain to use.
## </param>
#
interface(`ddcprobe_run',`
	gen_require(`
		type ddcprobe_t;
	')

	ddcprobe_domtrans($1)
	role $2 types ddcprobe_t;
	allow ddcprobe_t $3:chr_file rw_term_perms;
')
